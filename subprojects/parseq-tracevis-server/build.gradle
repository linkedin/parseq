ext {
  description = """Serves the tracevis tool for rendering graphviz diagrams"""
}

def jettyVersion = '9.3.0.v20150612'

configurations {
  tracevisTarGz
}

dependencies {
  compile project(":parseq-exec")
  compile project(":parseq-http-client")

  compile project(path: ':parseq-tracevis', configuration: 'tracevisArtifacts')
  // If you didn't change any code in tracevis module, you can use below dependencies to build current module
  // compile group: 'com.linkedin.parseq', name: 'parseq-tracevis', version: version, ext: 'tar.gz'
  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyVersion
  compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
  compile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.12'
}


task fatJar(type: Jar, dependsOn: ':parseq-tracevis:makeDist') {
  classifier = 'jar-with-dependencies'
  from configurations.compile.collect {
    if (it.isDirectory()) {
      it
    } else if (it.name.endsWith('tar.gz')){
      tarTree(it)
    } else {
      zipTree(it)
    }
  }

  with jar
  manifest {
    attributes("Created-By": "Gradle",
        "Version": version,
        "Built-By": project.rootProject.ext.builtByUser,
        "Build-JDK": JavaVersion.current())
    attributes 'Main-Class': 'com.linkedin.parseq.TracevisServerJarMain'
  }
}

artifacts {
  archives fatJar
}
