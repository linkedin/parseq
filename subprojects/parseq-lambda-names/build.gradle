ext {
  description = """Finds source code locations and infers operations for lambda expressions"""
}

apply plugin: 'com.github.johnrengelman.shadow'


configurations {
  testRuntime.extendsFrom shadow
}

dependencies {
  shadow group: 'com.linkedin.agentloader', name: 'linkedin-agent-loader', version: '1.0.4'
  shadow group: 'org.ow2.asm', name: 'asm-all', version: '5.1'
  testCompile group: 'org.testng', name: 'testng', version: '6.9.9'
}

sourceSets.main.compileClasspath += configurations.shadow + sourceSets.main.compileClasspath

javadoc.classpath += configurations.shadow + sourceSets.main.compileClasspath

shadowJar {
  configurations += [project.configurations.shadow]
  classifier = null
  relocate 'org.objectweb', 'parseq.org.objectweb'
}

jar {
  finalizedBy shadowJar // The shadowJar task basically overwrites the output of the jar task (kind of hacky)
  manifest {
    attributes 'Agent-Class': 'com.linkedin.parseq.lambda.ASMBasedTaskDescriptor$Agent'
  }
}

compileJava.options.compilerArgs += '-Xlint:-unchecked'